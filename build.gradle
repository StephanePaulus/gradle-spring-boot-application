import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.7.7' apply false
    id 'java'
    id 'com.diffplug.spotless' version "6.19.0"
    id 'io.freefair.lombok' version "8.0.1"
    id 'pl.allegro.tech.build.axion-release' version "1.15.5"
    id 'com.google.cloud.tools.jib' version "3.3.2"
    id 'jacoco-report-aggregation'
    id 'org.sonarqube' version "4.4.1.3373"
}

scmVersion {
    snapshotCreator({ version, position -> {
        if(position.branch != 'main'){
            "-alpha"
        }else{
            "-rc-"+position.shortRevision
        }
    }})
    versionCreator('versionWithBranch')
    repository {
        pushTagsOnly = true
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = true
    }
    versionIncrementer('incrementMinor')
}

group = 'org.example'
version = scmVersion.version
description = 'simple spring-boot gradle application'

repositories {
    mavenCentral()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

spotless {
    java {
        removeUnusedImports()
        palantirJavaFormat().style("GOOGLE")
    }
}

jib {
    from {
        image = "stephanepaulus/base-java17:main"
    }
    to {
        image = "stephanepaulus/spring-boot-application"
        tags = [version]
    }
}

sonar {
    properties {
        property "sonar.projectKey", "StephanePaulus_gradle-spring-boot-application"
        property "sonar.organization", "stephanepaulus"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.gradle.skipCompile", true
        if(System.getenv("PR_KEY") != null) {
            property "sonar.pullrequest.key", System.getenv("PR_KEY")
            property "sonar.pullrequest.branch", System.getenv("PR_BRANCH")
            property "sonar.pullrequest.base", System.getenv("PR_BASE")
        }
    }
}